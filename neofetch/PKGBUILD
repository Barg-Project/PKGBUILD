# Maintainer: Prograpper <ermanzero2008@gmail>

pkgname=neofetch
pkgver=r5278.1a6134b2.barg
pkgrel=1
pkgdesc="A CLI system information tool written in BASH that supports displaying images."
arch=("x86_64")
url="https://github.com/Barg-Project/neofetch"
license=('MIT')
depends=('bash' 'ttf-material-design-icons-extended')
makedepends=('git')
backup=('etc/neofetch/config.conf')
optdepends=(
  'catimg: Display Images'
  'chafa: Image to text support'
  'feh: Wallpaper Display'
  'imagemagick: Image cropping / Thumbnail creation / Take a screenshot'
  'jp2a: Display Images'
  'libcaca: Display Images'
  'nitrogen: Wallpaper Display'
  'w3m: Display Images'
  'xdotool: See https://github.com/dylanaraps/neofetch/wiki/Images-in-the-terminal'
  'xorg-xdpyinfo: Resolution detection (Single Monitor)'
  'xorg-xprop: Desktop Environment and Window Manager'
  'xorg-xrandr: Resolution detection (Multi Monitor + Refresh rates)'
  'xorg-xwininfo: See https://github.com/dylanaraps/neofetch/wiki/Images-in-the-terminal'
)

source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD).barg"
}

package() {
	  cd ${srcdir}/${pkgname}
	  make DESTDIR="$pkgdir" install
	  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

